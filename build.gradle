buildscript {
    ext.kotlin_version = '2.2.21'
    ext.boot_version = "3.3.13"
    ext.koog_version = "0.5.4"
    ext.ktor_version = "3.2.2"
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "$kotlin_version"
    id "org.jetbrains.kotlin.plugin.serialization" version "$kotlin_version"
    id "idea"
    id "java"
    id "maven-publish"
}

ext {
    coroutinesVersion = "1.10.2"
    serializationVersion = "1.8.1"
}

group 'io.opencui'
version '1.1-SNAPSHOT'


compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict", "-Xlambdas=class"]
        jvmTarget = "17"
    }
}
compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict", "-Xlambdas=class"]
        jvmTarget = "17"
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    all {
        exclude group: "org.slf4j", module: "slf4j-simple"
        exclude group: "org.apache.logging.log4j", module: "log4j-to-slf4j"
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == "org.jetbrains.kotlinx") {
                if (details.requested.name.startsWith("kotlinx-coroutines")) {
                    details.useVersion(coroutinesVersion)
                } else if (details.requested.name.startsWith("kotlinx-serialization")) {
                    details.useVersion(serializationVersion)
                }
            }
        }
    }

    // Add this to enforce logback as your only logging implementation
    all*.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    all*.exclude group: 'log4j', module: 'log4j'
}

dependencies {
    implementation 'org.jetbrains:annotations:20.1.0'
    compileOnly "javax.annotation:javax.annotation-api:1.2"
    compileOnly "org.slf4j:slf4j-api:2.0.16"
    implementation 'ch.qos.logback:logback-core:1.5.16'
    implementation("ch.qos.logback:logback-classic:1.5.16"){
        exclude(group: 'org.slf4j', module: 'slf4j-simple')
        exclude(group: "org.apache.logging.log4j", module: "log4j-to-slf4j")
    }
    implementation "org.clojure:clojure:1.12.0"
    implementation("org.springframework.boot:spring-boot-starter-webflux:${boot_version}"){
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
        exclude group: 'com.google.guava', module: 'guava'
    }

    api "ai.koog:koog-agents:$koog_version"
    // expose Ktor client stack required by Koog Google client
    api platform("io.ktor:ktor-bom:${ktor_version}")
    api "io.ktor:ktor-client-core"
    api "io.ktor:ktor-client-core-jvm"
    api "io.ktor:ktor-client-cio"
    api "io.ktor:ktor-client-cio-jvm"

    implementation "org.clojure:data.json:2.5.0"
    implementation "org.apache.lucene:lucene-core:8.4.1"
    implementation "org.apache.lucene:lucene-analyzers-smartcn:8.4.1"
    implementation "org.apache.lucene:lucene-queryparser:8.4.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:${coroutinesVersion}"
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-rx3:${coroutinesVersion}") // Use the latest compatible version
    implementation 'org.postgresql:postgresql:42.7.2'
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jdk8"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:${kotlin_version}"
    implementation "org.jetbrains.kotlin:kotlin-reflect:${kotlin_version}"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:${serializationVersion}"
    api 'com.fasterxml.jackson.module:jackson-module-jsonSchema'
    implementation 'org.apache.kafka:kafka-clients:3.8.1'

     constraints {
        implementation("org.slf4j:slf4j-api:2.0.16") {
            because("Ensure SLF4J version consistency")
        }
        implementation("ch.qos.logback:logback-classic:1.5.16") {
            because("Ensure Logback is the only SLF4J binding")
        }
    }

    // We need to stick to older version of h2 so that test can be done faster enough.
    // New h2 requires tcp connection, much slower. That is why we can NOT enforcePlatform
    // to latest SpringBoot
    testImplementation "com.h2database:h2:1.4.197"
    testImplementation "org.jetbrains.kotlin:kotlin-test"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit"
    testImplementation files('libs/duckling-0.4.24-standalone.jar')
}


task deployLocalJar(type: Copy) {
    from "libs/duckling-0.4.24-standalone.jar" // Replace with the actual path to your JAR file
    into "${System.properties['user.home']}/.m2/repository/io/opencui/duckling/0.4.24-standalone"
}


tasks.register("runKoog", JavaExec) {
  group = "verification"
  description = "Runs the Koog structured-output sample."
  classpath = sourceSets.test.runtimeClasspath
  mainClass = "io.opencui.system1.KoogKt"
}

